# U-Boot: ODROID-X/X2/U2/U3
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=uboot-odroid
pkgver=2014.10
pkgrel=1
pkgdesc="U-Boot for ODROID-X/X2/U2/U3"
arch=('armv7h')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
backup=('boot/uEnv.txt')
makedepends=('bc' 'dtc' 'git')
_commit=dd9a970aa4accf5d266d334c0f319c674e933027
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/bl1.HardKernel"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/bl2.HardKernel"
        "https://github.com/hardkernel/u-boot/raw/${_commit}/sd_fuse/tzsw.HardKernel"
        "sd_fusing.sh"
        '0001-arch-linux-arm-modifications.patch'
        'uEnv.txt')
md5sums=('3ddcaee2f05b7c464778112ec83664b5'
         '3ab6d3cc2061bc2590d60320254017c6'
         '841502de02bd42f2898e36c89c260b0f'
         'c38faafa02a6a1ae834457f378c82113'
         '17909ae93a0ae7b4c155234040105035'
         '565256b2d83b05906074dc40ed3b8e8c'
         '08921d3ead5fa3e97dfc1b8e50334677')

prepare() {
  cd u-boot-${pkgver}

  git apply ../0001-arch-linux-arm-modifications.patch
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS
  unset CXXFLAGS
  unset CPPFLAGS

  make distclean 
  make odroid_config
  make EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${pkgver}
  mkdir -p ${pkgdir}/boot
  cp u-boot-dtb.bin ${pkgdir}/boot/u-boot.bin
  cp ../uEnv.txt ${pkgdir}/boot
  cp ../{{bl1,bl2,tzsw}.HardKernel,sd_fusing.sh} ${pkgdir}/boot
}
